version = '13.1.3'
group = 'me.deftware'

buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven {
            name = "SpongePowered"
            url = "http://repo.spongepowered.org/maven"
        }
        maven {
            name = "GradlePlugins"
            url = "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "org.spongepowered:mixingradle:0.5-SNAPSHOT"
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: "java"
apply plugin: "org.spongepowered.mixin"
apply plugin: "com.github.johnrengelman.shadow"

sourceCompatibility = targetCompatibility = 1.8
compileJava {
    sourceCompatibility = targetCompatibility = 1.8
}
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = "1.12.2-14.23.2.2611"
    runDir = "run"
    mappings = "snapshot_20170906"
    useDepAts = true
    makeObfSourceJar = true
}

repositories {
    mavenCentral()

    maven {
        name = 'spongepowered-repo'
        url = 'http://repo.spongepowered.org/maven/'
    }
    maven {
        name = 'jitpack'
        url = 'https://jitpack.io'
    }

}

configurations {
    deps
    compile {
        extendsFrom deps
    }
}

dependencies {
    compile('org.spongepowered:mixin:0.7.5-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
    }
}

mixin {
    defaultObfuscationEnv searge
    add sourceSets.main, "mixins.client.refmap.json"
}

shadowJar {
    dependencies {
        include(dependency('org.spongepowered:mixin'))
    }

    classifier = 'full'
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
        classpath = sourceSets.main.compileClasspath
    }
}

build.dependsOn(shadowJar)

jar {
    manifest.attributes(
            "MixinConfigs": 'mixins.client.json',
            "tweakClass": 'org.spongepowered.asm.launch.MixinTweaker',
            "TweakOrder": 0,
            "FMLCorePluginContainsFMLMod": 'me.deftware.forge.MixinLoader',
            "Manifest-Version": 1.0
    )
}