plugins {
	id 'dev.architectury.loom' version '1.4-SNAPSHOT'
	id "io.freefair.lombok" version "8.6"
	id 'maven-publish'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

def forgeBuild = project.properties["loom.platform"] == "forge"

def getVersionFromJava() {
	def ver = 0.0, patch = 0
	def versionClass = file("src/main/java/me/deftware/client/framework/FrameworkConstants.java").text
	(versionClass =~ ~/double\s+([_a-zA-Z\u0024][_a-zA-Z0-9\u0024]*)\s+=\s+(([0-9]+)\.([0-9]+))/).each {
		ver = it[2]
	}
	(versionClass =~ ~/int\s+([_a-zA-Z$][_a-zA-Z0-9$]*)\s+=\s+([0-9]+)/).each {
		if (it[1].toString() == "PATCH") {
			patch = it[2]
		}
	}
	return "${ver}.${patch}".toString()
}

version = getVersionFromJava()

repositories {
	maven { url 'https://gitlab.com/EMC-Framework/maven/raw/master/' }
}

dependencies {
	if (forgeBuild) {
		forge "net.minecraftforge:forge:${project.forge_version}"
	} else {
		// include "me.deftware:integrations:${project.minecraftVersion}"
	}
	minecraft "com.mojang:minecraft:${project.minecraftVersion}"
	mappings "net.fabricmc:yarn:${project.minecraftVersion}${project.mappingsVer}:v2"
	modImplementation "net.fabricmc:fabric-loader:0.15.6"

	// No longer included in Minecraft
	implementation 'io.netty:netty-handler-proxy:4.1.76.Final'
	implementation 'com.google.code.findbugs:jsr305:3.0.2'
}

loom {
	if (forgeBuild) {
		forge {
			mixinConfig "mixins.emc.json"
			convertAccessWideners = true
		}
	}
	accessWidenerPath = file("src/main/resources/emc.accesswidener")
}

if (forgeBuild) {
	jar {
		manifest {
			attributes([
					'MixinConnector': 'me.deftware.launch.MixinConnector'
			])
		}
	}
} else {
	sourceSets {
		main {
			java {
				srcDir 'src'
				exclude '**/deftware/client/framework/main/ForgeMod**'
				exclude '**/deftware/launch/MixinConnector**'
			}
		}
	}
}

publishing {
	publications {
		emc(MavenPublication) {
			groupId 'me.deftware'
			if (forgeBuild) {
				artifactId "EMC-Forge"
			} else {
				artifactId "EMC-F-v2"
			}
			version "${version}-${project.minecraftVersion}"

			from components['java']
		}
	}

	repositories {
		maven {
			url "$rootDir/maven/"
		}
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

task generateApiDocumentation(type: Javadoc) {
	source = ['src/main/java/me/deftware/client']
	title = "EMC Framework Documentation"
	destinationDir = file("docs")
	failOnError false
}
