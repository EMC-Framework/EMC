import org.gradle.internal.os.OperatingSystem

plugins {
	id 'fabric-loom' version '0.8-SNAPSHOT'
	id "io.freefair.lombok" version "6.0.0-m2"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_16

def getVersionFromJava() {
	def ver = 0.0, patch = 0
	def versionClass = file("src/main/java/me/deftware/client/framework/FrameworkConstants.java").text
	(versionClass =~ ~/double\s+([_a-zA-Z\u0024][_a-zA-Z0-9\u0024]*)\s+=\s+(([0-9]+)\.([0-9]+))/).each {
		ver = it[2]
	}
	(versionClass =~ ~/int\s+([_a-zA-Z$][_a-zA-Z0-9$]*)\s+=\s+([0-9]+)/).each {
		if (it[1].toString() == "PATCH") {
			patch = it[2]
		}
	}
	return "${ver}.${patch}".toString()
}

def minecraftRoot = System.getenv('APPDATA') + "/.minecraft"
if (OperatingSystem.current().isMacOsX()) {
	minecraftRoot = System.getenv('HOME') + "/Library/Application Support/minecraft"
} else if (OperatingSystem.current().isLinux()) {
	minecraftRoot = System.getenv('HOME') + "/.minecraft"
}
version = getVersionFromJava()

repositories {
	maven { url = 'https://gitlab.com/EMC-Framework/maven/raw/master/' }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraftVersion}"
	mappings "net.fabricmc:yarn:${project.minecraftVersion}${project.mappingsVer}:v2"

	modImplementation "net.fabricmc:fabric-loader:0.11.3"
	modImplementation 'com.github.modmuss50:Fabric-ASM:b97939c03a'

	implementation 'com.google.code.findbugs:jsr305:3.0.2'
	implementation files("deps/mocked.jar")

	include 'com.github.modmuss50:Fabric-ASM:b97939c03a'
}

sourceSets {

	mock {
		compileClasspath += main.compileClasspath
	}

}

task mockJar(type: Jar) {
	archiveFileName = "mocked.jar"
	from sourceSets.mock.output
}

minecraft {
	refmapName = 'mixins.emc.refmap.json'
}

task createPom {
	doLast {
		pom {
			//noinspection GroovyAssignabilityCheck
			project {
				groupId "me.deftware"
				artifactId "EMC-F-v2"
				version "${version}-${project.minecraftVersion}"
			}
		}.writeTo("maven/me/deftware/EMC-F-v2/${version}-${project.minecraftVersion}/EMC-F-v2-${version}-${project.minecraftVersion}.pom")
	}
}

task generateApiDocumentation(type: Javadoc) {
	source = ['src/main/java/me/deftware/client']
	title = "EMC Framework Documentation"
	destinationDir = file("docs")
	failOnError false
}

task sourcesJar(type: Jar, dependsOn: classes) {
	archiveClassifier.set('sources')
	from sourceSets.main.allSource
}

task copyBuilds(type: Copy) {
	from "$rootDir/build/libs/EMC-${version}.jar"
	into "$rootDir/maven/me/deftware/EMC-F-v2/${version}-${project.minecraftVersion}/"
	rename { String fileName ->
		fileName.replace(version.toString(), "${version}-${project.minecraftVersion}".toString()).replace("EMC", "EMC-F-v2")
	}
	doLast {
		project.ant.checksum(file: "$rootDir/maven/me/deftware/EMC-F-v2/${version}-${project.minecraftVersion}/EMC-F-v2-${version}-${project.minecraftVersion}.jar", algorithm: "sha1", todir: "$rootDir/maven/me/deftware/EMC-F-v2/${version}-${project.minecraftVersion}/")
	}
}

task copyToMinecraft(type: Copy) {
	from "$rootDir/build/libs/EMC-${version}.jar"
	into "${minecraftRoot}/libraries/me/deftware/EMC-F-v2/${version}-${project.minecraftVersion}/"
	rename { String fileName ->
		fileName.replace("EMC", "EMC-F-v2").replace(".jar", "-${project.minecraftVersion}.jar".toString())
	}
}

build.dependsOn(createPom)
build.finalizedBy(copyBuilds)
copyToMinecraft.dependsOn(build)
